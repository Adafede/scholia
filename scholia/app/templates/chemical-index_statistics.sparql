SELECT ?count ?description
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P2102 []. }
} AS %boilingpoints
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P2101 []. }
} AS %meltingpoints
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P1117 []. }
} AS %pkas
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P231 []. }
} AS %cas
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P235 []. }
} AS %inchikeys
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P3636 | wdt:P11375 []. }
} AS %crystalStructures
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P6689 []. }
} AS %massSpectra
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P9405 []. }
} AS %nmrSpectra
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P2201 []. }
} AS %dipole
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P2260 []. }
} AS %ionizationPotential
WITH {
  SELECT (COUNT(*) AS ?count) WHERE { [] wdt:P4952 []. }
} AS %safetyInfo
WHERE {
  {
    INCLUDE %meltingpoints
    BIND("Total number of chemicals with a melting point" AS ?description)
  }
  UNION
  {
    INCLUDE %boilingpoints
    BIND("Total number of chemicals with a boiling point" AS ?description)
  }
  UNION
  {
    INCLUDE %pkas
    BIND("Total number of chemicals with a pKa" AS ?description)
  }
  UNION
  {
    INCLUDE %cas
    BIND("Total number of CAS registry numbers" AS ?description)
  }
  UNION
  {
    INCLUDE %inchikeys
    BIND("Total number of InChIKeys" AS ?description)
  }
  UNION
  {
    INCLUDE %crystalStructures
    BIND("Total number of chemicals with crystal structures" AS ?description)
  }
  UNION
  {
    INCLUDE %massSpectra
    BIND("Total number of chemicals with mass spectra" AS ?description)
  }
  UNION
  {
    INCLUDE %nmrSpectra
    BIND("Total number of chemicals with NMR spectra" AS ?description)
  }
  UNION
  {
    INCLUDE %dipole
    BIND("Total number of chemicals with a electronic dipole moment" AS ?description)
  }
  UNION
  {
    INCLUDE %ionizationPotential
    BIND("Total number of chemicals with a ionization potential" AS ?description)
  }
  UNION
  {
    INCLUDE %safetyInfo
    BIND("Total number of chemicals with a safety classification and labelling" AS ?description)
  }
}
ORDER BY DESC(?count)
