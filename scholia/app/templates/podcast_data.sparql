PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?description ?value ?valueUrl
WHERE {
  BIND(target: AS ?podcast)
  {
    BIND(1 AS ?order)
    BIND("Title" AS ?description)
    ?podcast wdt:P1476 ?value .
  }
  UNION
  {
    SELECT
      (2 AS ?order)
      ("Presenters" AS ?description)
      (GROUP_CONCAT(?value_; separator=", ") AS ?value)
      (CONCAT("../authors/", GROUP_CONCAT(?q; separator=",")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P371 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(3 AS ?order)
    BIND("Language" AS ?description)
    ?podcast wdt:P407 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(STR(?value_string) AS ?value)
    BIND(CONCAT("../language/", ?q) AS ?valueUrl)
  }
  UNION
  {
    SELECT ?description ?order (MAX(?value_) AS ?value) WHERE {
      BIND(target: AS ?podcast)
      ?podcast wdt:P1113 ?value_ .
      BIND(4 AS ?order)
      BIND("Number of episodes" AS ?description)
    } GROUP BY ?description ?order
  }
  UNION
  {
    BIND(5 AS ?order)
    BIND("Web feed" AS ?description)
    ?podcast wdt:P1019 ?valueUrl .
    BIND("feed" AS ?value)
  }
  UNION
  {
    BIND(6 AS ?order)
    BIND("Apple Podcasts" AS ?description)
    ?podcast wdt:P5842 ?valueUrl_ .
    BIND(CONCAT("https://podcasts.apple.com/podcast/id", ENCODE_FOR_URI(?valueUrl_)) AS ?valueUrl)
    BIND(CONCAT(?valueUrl_, " ↗") AS ?value)
  }
  UNION
  {
    BIND(7 AS ?order)
    BIND("Deezer" AS ?description)
    ?podcast wdt:P5988 ?valueUrl_ .
    BIND(CONCAT("https://www.deezer.com/show/", ENCODE_FOR_URI(?valueUrl_)) AS ?valueUrl)
    BIND(CONCAT(?valueUrl_, " ↗") AS ?value)
  }
  UNION
  {
    BIND(8 AS ?order)
    BIND("Podchaser" AS ?description)
    ?podcast wdt:P7998 ?valueUrl_ .
    BIND(CONCAT("https://www.podchaser.com/podcasts/", ENCODE_FOR_URI(?valueUrl_)) AS ?valueUrl)
    BIND(CONCAT(?valueUrl_, " ↗") AS ?value)
  }
  UNION
  {
    BIND(9 AS ?order)
    BIND("Spotify" AS ?description)
    ?podcast wdt:P5916 ?valueUrl_ .
    BIND(CONCAT("https://open.spotify.com/show/", ENCODE_FOR_URI(?valueUrl_)) AS ?valueUrl)
    BIND(CONCAT(?valueUrl_, " ↗") AS ?value)
  }
} 
ORDER BY ?order
