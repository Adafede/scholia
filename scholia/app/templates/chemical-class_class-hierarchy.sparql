#defaultView:Graph
PREFIX target: <http://www.wikidata.org/entity/{{ q }}>

SELECT DISTINCT ?class ?classLabel ?otherclass ?otherclassLabel ?rgb WITH {
  SELECT DISTINCT ?class ?otherclass ?rgb WHERE {
    # find the subclasses
    { VALUES ?class { target: }
      # subclasses that have subclasses themselves
      { ?otherclass wdt:P279 ?class . [] wdt:P279 ?otherclass  BIND( "3182BD" AS ?rgb) }
      UNION
      # subclasses that do not have subclasses
      { ?otherclass wdt:P279 ?class . MINUS { [] wdt:P279 ?otherclass } BIND( "E6550D" AS ?rgb) }
    }
    UNION
    # find the superclasses
    { VALUES ?otherclass { target: }
      ?otherclass wdt:P279 ?class .
    }
  } LIMIT 500
} AS %result
WHERE {
  INCLUDE %result
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
